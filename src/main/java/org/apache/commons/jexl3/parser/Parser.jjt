/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


options
{
   MULTI=true;
   STATIC=false;
   VISITOR=true;
   NODE_SCOPE_HOOK=true;
   NODE_CLASS="JexlNode";
   UNICODE_INPUT=true;
   KEEP_LINE_COLUMN=true;
   TRACK_TOKENS=true;
   CACHE_TOKENS=true;
   ERROR_REPORTING=false;
   //DEBUG_PARSER=true;
   //DEBUG_TOKEN_MANAGER=true;
}

PARSER_BEGIN(Parser)

package org.apache.commons.jexl3.parser;

import java.util.Collections;
import java.util.LinkedList;
import java.util.regex.Pattern;

import org.apache.commons.jexl3.JexlInfo;
import org.apache.commons.jexl3.JexlFeatures;
import org.apache.commons.jexl3.JexlException;
import org.apache.commons.jexl3.internal.Scope;

public final class Parser extends JexlParser
{
    public ASTJexlScript parse(JexlInfo jexlInfo, JexlFeatures jexlFeatures, String jexlSrc, Scope scope) {
        JexlFeatures previous = getFeatures();
        try {
            setFeatures(jexlFeatures);
            // If registers are allowed, the default parser state has to be REGISTERS.
            if (jexlFeatures.supportsRegister()) {
                token_source.defaultLexState = REGISTERS;
            }
            // lets do the 'Unique Init' in here to be safe - it's a pain to remember
            info = jexlInfo != null? jexlInfo : new JexlInfo();
            source = jexlSrc;
            pragmas = null;
            frame = scope;
            branchScope = new BranchScope();
            ReInit(new java.io.StringReader(jexlSrc));
            ASTJexlScript script = jexlFeatures.supportsScript()? JexlScript(scope) : JexlExpression(scope);
            script.jjtSetValue(info.detach());
            script.setFeatures(jexlFeatures);
            script.setPragmas(pragmas != null
                             ? Collections.<String,Object>unmodifiableMap(pragmas)
                             : Collections.<String,Object>emptyMap());
            return script;
        } catch (TokenMgrError xtme) {
            throw new JexlException.Tokenization(info, xtme).clean();
        } catch (ParseException xparse) {
            Token errortok = errorToken(jj_lastpos, jj_scanpos, token.next, token);
            throw new JexlException.Parsing(info.at(errortok.beginLine, errortok.beginColumn), errortok.image).clean();
        } finally {
            token_source.defaultLexState = DEFAULT;
            cleanup(previous);
        }
    }
}

PARSER_END(Parser)

TOKEN_MGR_DECLS : {
    /**
     *   A stack of 1 for keeping state to deal with dotted identifiers
     */
    int dotLexState = DEFAULT;

    public void pushDot() {
        dotLexState = curLexState;
        curLexState = DOT_ID;
    }

    public void popDot() {
        if (curLexState == DOT_ID) {
            curLexState = dotLexState;
            dotLexState = defaultLexState;
        }
    }

    public void pushQ() {
        dotLexState = curLexState;
        curLexState = QUALIFIED;
    }

    public void popQ() {
        if (curLexState == QUALIFIED) {
            curLexState = dotLexState;
            dotLexState = defaultLexState;
        }
    }

}
/***************************************
 *     Skip & Number literal tokens
 ***************************************/

<*> SKIP : /* WHITE SPACE */
{
      <"##" (~["\n","\r"])* ("\n" | "\r" | "\r\n")? >
    | <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
    | <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")? >
    | " "
    | "\t"
    | "\n"
    | "\r"
    | "\f"
    | "\u00A0"
}

<*> TOKEN : /* KEYWORDS */
{
      < IF : "if" > { popDot(); }
    | < ELSE : "else" > { popDot(); }
    | < FOR : "for" > { popDot(); }
    | < WHILE : "while" > { popDot(); }
    | < DO : "do" > { popDot(); }
    | < SWITCH : "switch" > { popDot(); }
    | < CASE : "case" > { popDot(); }
    | < DCASE : "default" > { popDot(); }
    | < TRY : "try" > { popDot(); }
    | < CATCH : "catch" > { popDot(); }
    | < FINALLY : "finally" > { popDot(); }
    | < THROW : "throw" > { popDot(); }
    | < ASSERT : "assert" > { popDot(); }
    | < SYNCHRONIZED : "synchronized" > { popDot(); }
    | < NEW : "new" > { popDot(); pushQ(); } /* Lexical state is now QUALIFIED */
    | < VAR : "var" > { popDot(); } /* Revert state to default if was DOT_ID. */
    | < FINAL : "final" > { popDot(); }
    | < EMPTY : "empty" > { popDot(); } /* Revert state to default if was DOT_ID. */
    | < SIZE : "size" > { popDot(); } /* Revert state to default if was DOT_ID. */
    | < THIS : "this" > { popDot(); }
    | < NULL : "null" | "\u2205" > { popDot(); }
    | < TRUE : "true" > { popDot(); }
    | < FALSE : "false" > { popDot(); }
    | < RETURN : "return" > { popDot(); }
    | < YIELD : "yield" > { popDot(); }
    | < STATIC : "static" > { popDot(); }
    | < FUNCTION : "function" > { popDot(); } /* Revert state to default if was DOT_ID. */
    | < LAMBDA : "->" | "\u2192" >
    | < LAMBDAE : "=>" | "\u21D2" >
    | < BREAK : "break" > { popDot(); }
    | < CONTINUE : "continue" > { popDot(); }
    | < REMOVE : "remove" > { popDot(); } /* Revert state to default if was DOT_ID. */
    | < PRAGMA : "#pragma" > { pushQ(); } /* Lexical state is now QUALIFIED */
    | < UNDERSCORE : "_" > { popDot(); }
}

<*> TOKEN : { /* SEPARATORS */
      < LPAREN : "("> { popQ(); } /* Revert state to default. */
    | < RPAREN : ")"> { popQ(); } /* Revert state to default. */
    | < LCURLY : "{" >
    | < RCURLY : "}" >
    | < LBRACKET : "[" > { popQ(); } /* Revert state to default. */
    | < RBRACKET : "]" >
    | < SEMICOL : ";" > { popQ(); } /* Revert state to default. */
    | < COLON : ":" >
    | < DCOLON : "::" >
    | < COMMA : "," > { popQ(); } /* Revert state to default. */
    | < DOT : "." > { pushDot(); } /* Lexical state is now DOT_ID */
    | < QDOT : "?." > { pushDot(); } /* Lexical state is now DOT_ID */
    | < QLBRACKET : "?[" > { popDot(); } /* Revert state to default */
    | < ELLIPSIS : "..." | "\u2026">
    | < HBRACKET : "#[" >
    | < HCURLY : "#{" >
}

<*> TOKEN : { /* PRIMITIVE TYPES */
      < CHAR :    "char" > { popDot(); }
    | < BYTE :    "byte" > { popDot(); }
    | < SHORT :   "short" > { popDot(); }
    | < INT :     "int" > { popDot(); }
    | < LONG :    "long" > { popDot(); }
    | < FLOAT :   "float" > { popDot(); }
    | < DOUBLE :  "double" > { popDot(); }
    | < BOOLEAN : "boolean" > { popDot(); }
}

<*> TOKEN : { /* STREAMS */
      < DOTP : ".(" >
    | < DOTB : ".[" >
    | < DOTC : ".{" >
}

<*> TOKEN : { /* CONDITIONALS */
      < QMARK : "?" >
    | < ELVIS : "?:" >
    | < EN_ASSIGN : "=:" >
    | < NULLP : "??" >
    | < AND : "&&" | "\u2227" >
    | < _AND :  "and" >  { popDot(); }
    | < OR : "||" | "\u2228" >
    | < _OR: "or" > { popDot(); }
}

<*> TOKEN : { /* COMPARISONS */
      < eq : "==" | "\u2261">
    | < EQ : "eq" > { popDot(); }
    | < ne : "!=" | "\u2260" >
    | < NE : "ne" > { popDot(); }
    | < req : "=~" | "\u2208" > // regexp equal
    | < IN : "in" > { popDot(); }
    | < rne : "!~" | "!in" | "\u2209" > // regexp not equal
    | < is : "===" > // identitical
    | < ni : "!==" > // not identitical
    | < seq : "=^" > // starts equal
    | < eeq : "=$" > // ends equal
    | < sne : "!^" > // start not equal
    | < ene : "!$" > // ends not equal
    | < gt : ">" >
    | < GT : "gt" > { popDot(); }
    | < ge : ">=" | "\u2265" >
    | < GE : "ge" > { popDot(); }
    | < lt : "<" >
    | < LT : "lt" > { popDot(); }
    | < le : "<=" | "\u2264" >
    | < LE : "le"  > { popDot(); }
    | < iof : "instanceof" > { popDot(); pushQ(); }
    | < niof : "!instanceof" > { pushQ(); }

}

<*> TOKEN : { /* OPERATORS */
      < plus_assign : "+=" >
    | < minus_assign : "-=" >
    | < mult_assign : "*=" >
    | < div_assign : "/=" >
    | < mod_assign : "%=" >
    | < and_assign : "&=" >
    | < or_assign : "|=" >
    | < xor_assign : "^=" >
    | < shl_assign : "<<=" >
    | < sar_assign : ">>=" >
    | < shr_assign : ">>>=" >

    | < null_assign : "?=" >
    | < ne_assign : ":=" >
    | < assign : "=" >
    | < increment : "++" >
    | < decrement : "--" >
    | < plus : "+" >
    | < minus : "-" >
    | < unary_minus : "\u2212" >
    | < mult : "*" | "\u22C5" >
    | < div : "/" >
    | < DIV : "div" > { popDot(); }
    | < mod : "%" >
    | < MOD : "mod" > { popDot(); }
    | < not : "!" | "\u00AC" >
    | < NOT : "not" > { popDot(); }
    | < and : "&" >
    | < or : "|" >
    | < xor : "^" >
    | < shl : "<<" >
    | < shr : ">>>" >
    | < sar : ">>" >

    | < tilda : "~" >
    | < range : ".." | "\u2025" >
}

/***************************************
 *     Identifier & String tokens
 ***************************************/
<*> TOKEN :  /* NaN */
{
    < NAN_LITERAL : "NaN" >
}

<*> TOKEN : /* ANNOTATION */
{
  < ANNOTATION: "@" ( [ "0"-"9", "a"-"z", "A"-"Z", "_", "$" ])+ >
}

<*> TOKEN : /* CURRENT CONTEXT */
{
  < CURRENT: "@" > { popDot(); } /* Revert state to default. */
}

<DOT_ID> TOKEN : /* IDENTIFIERS */
{
  < DOT_IDENTIFIER: ( [ "0"-"9", "a"-"z", "A"-"Z", "_", "$", "@" ])+ > { popDot(); } /* Revert state to default. */
}

<DEFAULT, REGISTERS> TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>|<ESCAPE>)* > { matchedToken.image = StringParser.unescapeIdentifier(matchedToken.image); }
|
  < #LETTER: [ "a"-"z", "A"-"Z", "_", "$", "@" ] >
|
  < #DIGIT: [ "0"-"9"] >
|
  < #ESCAPE: "\\" [" ", "'", "\"", "\\"] >
}

<QUALIFIED> TOKEN : /* IDENTIFIERS */
{
  < QUALIFIED_IDENTIFIER: <QNAME> ("." <QNAME>)* > { popQ(); } /* Revert state to default. */
|
  < #QNAME: [ "a"-"z", "A"-"Z", "_", "$", "@" ] ([ "a"-"z", "A"-"Z", "_", "$", "@", "0"-"9" ])* >
}

<REGISTERS> TOKEN : /* REGISTERS: parser.ALLOW_REGISTER must be set to true before calling parse */
{
  < REGISTER: "#" (["0"-"9"])+ >
}

<DEFAULT, REGISTERS> TOKEN : /* LITERALS */
{
 <INTEGER_LITERAL:
      <DECIMAL_LITERAL> (<INT_SFX>)?
    | <HEX_LITERAL> (<INT_SFX>)?
    | <OCTAL_LITERAL> (<INT_SFX>)?
    | <BINARY_LITERAL> (<INT_SFX>)?
 >
    | <#DECIMAL_LITERAL: ["1"-"9"] ((["0"-"9","_"])* <DIGIT> | (<DIGIT>)*) >
    | <#HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"] (["0"-"9","a"-"f","A"-"F","_"])* ["0"-"9","a"-"f","A"-"F"] | (["0"-"9","a"-"f","A"-"F"])+) >
    | <#BINARY_LITERAL: "0" ["b","B"] (["0"-"1"] (["0"-"1","_"])* ["0"-"1"] | (["0"-"1"])+) >
    | <#OCTAL_LITERAL: "0" ((["0"-"7","_"])* ["0"-"7"] | (["0"-"7"])*) >
    | <#INT_SFX : ["l","L","h","H"]>
|
<FLOAT_LITERAL:
      <FLT_NUM> "." <FLT_NUM> (<FLT_SFX>)?
    | <FLT_NUM> (".")? (<FLT_SFX>)
    | "." <FLT_NUM> (<FLT_SFX>)
    | "#NaN"
>
    |  <#FLT_NUM: (<DIGIT> (["0"-"9","_"])* <DIGIT> | (<DIGIT>)+) >
    |  <#EXPONENT: ["e","E"] (["+","-"])? <FLT_NUM> >
    |  <#FLT_CLS : ["f","F","d","D","b","B"]>
    |  <#FLT_SFX : <EXPONENT> (<FLT_CLS>)? | <FLT_CLS> >
}

<*> TOKEN :
{
  < STRING_LITERAL:
    "\"" (~["\"","\\","\n","\r","\u2028","\u2029"] | "\\" ~["\n","\r","\u2028","\u2029"])* "\""
  |
    "\u201C" (~["\u201C","\u201D","\\","\n","\r","\u2028","\u2029"] | "\\" ~["\n","\r","\u2028","\u2029"])* "\u201D"
  |
    "\u2018" (~["\u2018","\u2019","\\","\n","\r","\u2028","\u2029"] | "\\" ~["\n","\r","\u2028","\u2029"])* "\u2019"
  |
    "'" (~["'","\\","\n","\r","\u2028","\u2029"] | "\\" ~["\n","\r","\u2028","\u2029"])* "'"
  > { popDot(); } /* Revert state to default if was DOT_ID. */
}

<*> TOKEN :
{
  < JXLT_LITERAL:
    "`" (~["`","\\"] | "\\" ~["\u0000"])* "`"
  > { popDot(); } /* Revert state to default if was DOT_ID. */
}

<*> TOKEN :
{
  < REGEX_LITERAL:
    "~" "/" (~["/","\n","\r","\u2028","\u2029"] | "\\" "/" )* "/"
  > { popDot(); } /* Revert state to default if was DOT_ID. */
}

/***************************************
 *      Statements
 ***************************************/

ASTJexlScript JexlScript(Scope frame) :
{
    jjtThis.setScope(frame);
    pushUnit(jjtThis);
}
{
    (LOOKAHEAD(<PRAGMA>) Pragma())*
    (
       LOOKAHEAD( LambdaLookahead() ) Lambda() [ <SEMICOL> ]
       |
       (GenericStatement())*
    ) <EOF>
{
    popUnit(jjtThis);
    jjtThis.setScope(getFrame());
    popFrame();
    return jjtThis.script();
}
}

ASTJexlScript JexlExpression(Scope frame) #JexlScript :
{
    jjtThis.setScope(frame);
    pushUnit(jjtThis);
}
{
    [ Expression() ] <EOF>
{
    popUnit(jjtThis);
    jjtThis.setScope(getFrame());
    popFrame();
    return jjtThis.script();
}
}

void Annotation() #Annotation :
{
    Token t;
}
{
    t=<ANNOTATION> (LOOKAHEAD(<LPAREN>) Arguments() )? { jjtThis.setName(t.image); }
}

void AnnotatedStatement() #AnnotatedStatement() : {}
{
    (LOOKAHEAD(<ANNOTATION>) Annotation())+ (LOOKAHEAD({!getFeatures().isLexical()}) GenericStatement() | Statement())
}

void GenericStatement() #void : {}
{
    <SEMICOL>
    | AnnotatedStatement()
    | LOOKAHEAD([ <STATIC> ] <FUNCTION> <IDENTIFIER>) FunctionStatement()
    | LOOKAHEAD(LabelledStatementLookahead()) LabelledStatement()
    | LOOKAHEAD(DeclareLocalVar()) Var()
    | LOOKAHEAD(SwitchStatementLookahead()) SwitchStatement()
    | LOOKAHEAD(Expression()) ExpressionStatement()
    | Block()
    | IfStatement()
    | LOOKAHEAD(<FOR> <LPAREN> ForeachVar() <COLON>) ForeachStatement()
    | ForStatement()
    | WhileStatement()
    | DoWhileStatement()
    | LOOKAHEAD(<TRY> <LPAREN>) TryWithResourceStatement()
    | TryStatement()
    | ThrowStatement()
    | AssertStatement()
    | SynchronizedStatement()
    | LOOKAHEAD(MultipleIdentifier() <assign>) MultipleAssignmentStatement()
    | ReturnStatement()
    | YieldStatement()
    | Continue()
    | Remove()
    | Break()
    | LOOKAHEAD(<VAR> <LPAREN> | <FINAL> <VAR> <LPAREN>) MultipleVar()
}

void Statement() #void : {}
{
    <SEMICOL>
    | AnnotatedStatement()
    | LOOKAHEAD(LabelledStatementLookahead()) LabelledStatement()
    | LOOKAHEAD(SwitchStatementLookahead()) SwitchStatement()
    | LOOKAHEAD(Expression()) ExpressionStatement()
    | Block()
    | IfStatement()
    | LOOKAHEAD(<FOR> <LPAREN> ForeachVar() <COLON>) ForeachStatement()
    | ForStatement()
    | WhileStatement()
    | DoWhileStatement()
    | LOOKAHEAD(<TRY> <LPAREN>) TryWithResourceStatement()
    | TryStatement()
    | ThrowStatement()
    | AssertStatement()
    | SynchronizedStatement()
    | LOOKAHEAD(MultipleIdentifier() <assign>) MultipleAssignmentStatement()
    | ReturnStatement()
    | YieldStatement()
    | Continue()
    | Remove()
    | Break()
}

void StatementBranch() #void : {}
{
    LOOKAHEAD(1) Block() | LOOKAHEAD({!getFeatures().isLexical()}) GenericStatement() | Statement()
}

void LabelledStatementLookahead() #void() : {}
{
  <IDENTIFIER> <COLON> ( <LCURLY> | <IF> | <FOR> | <WHILE> | <DO> | <SWITCH> | <SYNCHRONIZED> | <TRY>)
}

void LabelledStatement() #void :
{
    Token t = null;
    String label = null;
    ASTLabelledStatement s;
}
{
    t=<IDENTIFIER> { label = t.image; if (branchScope.breakSupported(label)) throwParsingException(null, t); } <COLON>
    (
        LOOKAHEAD(<LCURLY>) { branchScope.pushBlockLabel(label); } s = Block() { s.setLabel(label); branchScope.popBlockLabel(); }
        |
        LOOKAHEAD(<FOR> <LPAREN> ForeachVar() <COLON>) { branchScope.pushForeachLabel(label); } s = ForeachStatement() { s.setLabel(label); branchScope.popForeachLabel(); }
        |
        { branchScope.pushLoopLabel(label); } s = ForStatement() { s.setLabel(label); branchScope.popLoopLabel(); }
        |
        { branchScope.pushLoopLabel(label); } s = WhileStatement() { s.setLabel(label); branchScope.popLoopLabel(); }
        |
        { branchScope.pushLoopLabel(label); } s = DoWhileStatement() { s.setLabel(label); branchScope.popLoopLabel(); }
        |
        { branchScope.pushBlockLabel(label); } s = SwitchStatement() { s.setLabel(label); branchScope.popBlockLabel(); }
        |
        { branchScope.pushBlockLabel(label); } s = IfStatement() { s.setLabel(label); branchScope.popBlockLabel(); }
        |
        { branchScope.pushBlockLabel(label); } s = SynchronizedStatement() { s.setLabel(label); branchScope.popBlockLabel(); }
        |
        LOOKAHEAD(<TRY> <LPAREN>) { branchScope.pushBlockLabel(label); } s = TryWithResourceStatement() { s.setLabel(label); branchScope.popBlockLabel(); }
        |
        { branchScope.pushBlockLabel(label); } s = TryStatement() { s.setLabel(label); branchScope.popBlockLabel(); }
    )
}

ASTBlock Block() #Block :
{
    pushUnit(jjtThis);
}
{
    <LCURLY> (GenericStatement())* <RCURLY>
{
    popUnit(jjtThis);
    return jjtThis;
}
}

void ExpressionStatement() : {}
{
    Expression() (LOOKAHEAD(2) Expression() #Ambiguous(1))* (LOOKAHEAD(1) <SEMICOL>)?
}

void MultipleAssignmentStatement() #MultipleAssignment : {}
{
    MultipleIdentifier() <assign> Expression()
}

void MultipleIdentifier() #MultipleIdentifier : {}
{
    <LPAREN> AssignmentIdentifier() (<COMMA> AssignmentIdentifier())* <RPAREN>
}

void AssignmentIdentifier() #void : {}
{
    UnderscoreLiteral()
    |
    Identifier(true)
}

void FunctionStatement() :
{
    boolean isStatic = false;
}
{
    [ <STATIC> { isStatic = true; } ] <FUNCTION> FunctionVar() Function(isStatic)
}

void FunctionVar() #Var :
{
    Token t;
}
{
    t=<IDENTIFIER> { jjtThis.setFinal(); declareVariable(jjtThis, t); }
}

void Function(boolean isStatic) #JexlLambda() :
{
    pushFrame();
    pushUnit(jjtThis);
}
{
    { if (isStatic) frame.declareStatic(); }
    Parameters() ( Block() | <LAMBDAE> Expression() )
{
    popUnit(jjtThis);
    jjtThis.setScope(frame);
    popFrame();
}
}

void ExpressionLambda() #SimpleLambda :
{
    pushFrame();
    pushUnit(jjtThis);
}
{
    ConditionalExpression()
{
    popUnit(jjtThis);
    jjtThis.setScope(frame);
    popFrame();
}
}

ASTIfStatement IfStatement() : {}
{
    <IF> <LPAREN> Expression() <RPAREN> StatementBranch()
    [ LOOKAHEAD(1) <ELSE> StatementBranch() ]
{
    return jjtThis;
}
}

ASTWhileStatement WhileStatement() : {}
{
    <WHILE> <LPAREN> Expression() <RPAREN>  { branchScope.loopCount += 1; } StatementBranch() { branchScope.loopCount -= 1; }
{
    return jjtThis;
}
}

ASTDoWhileStatement DoWhileStatement() : {}
{
    <DO> { branchScope.loopCount += 1; } StatementBranch() <WHILE> <LPAREN> Expression() <RPAREN> { branchScope.loopCount -= 1; }
{
    return jjtThis;
}
}

ASTTryStatement TryStatement() :
{
    branchScope.yieldReturnCount -= 1;
}
{
    <TRY> Block()
    (
        (CatchBlock())+ [ <FINALLY> Block() ]
        |
        <FINALLY> Block()
    )
{
    branchScope.yieldReturnCount += 1;
    return jjtThis;
}
}

ASTTryWithResourceStatement TryWithResourceStatement() :
{
    pushUnit(jjtThis);
    branchScope.yieldReturnCount -= 1;
}
{
    <TRY> <LPAREN> TryResource() <RPAREN> Block()
    {
      popUnit(jjtThis);
    }
    (CatchBlock())* [ <FINALLY> Block() ]
{
    branchScope.yieldReturnCount += 1;
    return jjtThis;
}
}

void CatchBlock() :
{
    pushUnit(jjtThis);
}
{
    <CATCH> <LPAREN> CatchVar() <RPAREN> Block()
{
    popUnit(jjtThis);
}
}

void TryResource() : {}
{
    LOOKAHEAD(TryVar() <assign>) TryVar() <assign> Expression()
    |
    Expression()
}

void TryVar() : {}
{
    DeclareTryVar()
    |
    Identifier(true)
}

void CatchVar() #void : {}
{
    DeclareMultiCatchVar()
    |
    DeclareCatchVar()
    |
    Identifier(true)
}

void ThrowStatement() : {}
{
    <THROW> Expression()
}

void AssertStatement() : {}
{
    <ASSERT> Expression() [ <COLON> Expression() ]
}

ASTSynchronizedStatement SynchronizedStatement() :
{
    branchScope.yieldReturnCount -= 1;
}
{
    <SYNCHRONIZED> <LPAREN> Expression() <RPAREN> Block()
{
    branchScope.yieldReturnCount += 1;
    return jjtThis;
}
}

void SwitchStatementLookahead() : {}
{
    <SWITCH> <LPAREN> Expression() <RPAREN> <LCURLY> (<CASE> SwitchCaseLabel() <COLON> | <DCASE> <COLON>)
}

ASTSwitchStatement SwitchStatement() :
{
    pushUnit(jjtThis);
}
{
    <SWITCH> <LPAREN> Expression() <RPAREN> { branchScope.switchCount += 1; } <LCURLY> SwitchStatementBlock() <RCURLY> { branchScope.switchCount -= 1; }
{
    popUnit(jjtThis);
    return jjtThis;
}
}

void SwitchStatementBlock() #void : {}
{
    SwitchStatementCase() (LOOKAHEAD(SwitchStatementBlock()) SwitchStatementBlock())*
    |
    SwitchStatementDefault() (LOOKAHEAD(SwitchStatementCase()) SwitchStatementCase())*
}

void SwitchStatementCase() : {}
{
    <CASE> SwitchCaseLabel() <COLON> (GenericStatement())*
}

void SwitchStatementDefault() : {}
{
    <DCASE> <COLON> (GenericStatement())*
}

void ReturnStatement() : {}
{
    <RETURN> [ LOOKAHEAD(2) ExpressionStatement() ] { if (branchScope.yieldSupported()) { throwParsingException(jjtThis); } }
}

void YieldStatement() : {}
{
    <YIELD> Expression() { if (!branchScope.yieldSupported()) { throwParsingException(jjtThis); }; if (branchScope.yieldCount == 0) jjtThis.setReturn(); }
}

void Continue() #Continue :
{
    Token t = null;
}
{
    <CONTINUE> [ LOOKAHEAD(<IDENTIFIER>) t = <IDENTIFIER> { if (!branchScope.continueSupported(t.image)) throwParsingException(jjtThis); jjtThis.setLabel(t.image); } ]
    { if (t == null && !branchScope.continueSupported()) { throwParsingException(jjtThis); } }
}

void Remove() #Remove :
{
    Token t = null;
}
{
    <REMOVE> [ LOOKAHEAD(<IDENTIFIER>) t = <IDENTIFIER> { if (!branchScope.removeSupported(t.image)) throwParsingException(jjtThis); jjtThis.setLabel(t.image); } ]
    { if (t == null && !branchScope.removeSupported()) { throwParsingException(jjtThis); } }
}

void Break() #Break :
{
    Token t = null;
}
{
    <BREAK> [ LOOKAHEAD(<IDENTIFIER>) t = <IDENTIFIER> { if (!branchScope.breakSupported(t.image)) throwParsingException(jjtThis); jjtThis.setLabel(t.image); } ]
    { if (t == null && !branchScope.breakSupported()) { throwParsingException(jjtThis); } }
}

ASTForStatement ForStatement() :
{
    pushUnit(jjtThis);
}
{
    <FOR> <LPAREN> ForInitializationNode() <SEMICOL> ForTerminationNode() <SEMICOL> ForIncrementNode() <RPAREN> { branchScope.loopCount += 1; } StatementBranch() { branchScope.loopCount -= 1; }
{
    popUnit(jjtThis);
    return jjtThis;
}
}

void ForInitializationExpression() #void : {}
{
    LOOKAHEAD(DeclareLocalVar()) Var() | Expression()
}

void ForInitializationNode() : {}
{
    [ ForInitializationExpression() (<COMMA> ForInitializationExpression() )* ]
}

void ForTerminationNode() : {}
{
    [ ConditionalExpression() ]
}

void ForIncrementNode() : {}
{
    [ Expression() (<COMMA> Expression())* ]
}

ASTForeachStatement ForeachStatement() :
{
    pushUnit(jjtThis);
}
{
    <FOR> <LPAREN> ForeachVar() <COLON> Expression() <RPAREN> { branchScope.foreachLoopCount += 1; } StatementBranch() { branchScope.foreachLoopCount -= 1; }
{
    popUnit(jjtThis);
    return jjtThis;
}
}

void ForeachVar() : {}
{
    DeclareVar() [ <COMMA> DeclareExtVar(false) ]
    |
    Identifier(true) [ <COMMA> Identifier(true) ]
}

void MultipleVar() #MultipleInitialization : {}
{
    MultipleDeclareVar() <assign> Expression()
}

void MultipleDeclareVar() #MultipleIdentifier :
{
    boolean isFinal = false;
}
{
    [ <FINAL> { isFinal = true; jjtThis.setFinal(); } ] <VAR> <LPAREN> DeclareExtVar(isFinal) (<COMMA> DeclareExtVar(isFinal))* <RPAREN>
}

void DeclareVar() #Var :
{
    Token t;
}
{
    <VAR> t=<IDENTIFIER> { declareVariable(jjtThis, t); }
}

void RequiredVarLookahead() #void() : {}
{
    <FINAL>
    |
    <VAR> <and>
    |
    (<BYTE> | <SHORT> | <INT> | <LONG> | <CHAR> | <BOOLEAN> | <FLOAT> | <DOUBLE>) ((<LBRACKET> <RBRACKET>)+ <and> <IDENTIFIER> | <IDENTIFIER>)
    |
    <IDENTIFIER> LOOKAHEAD( { Parser.resolveType(getToken(0).image) != null } ) (<LBRACKET> <RBRACKET>)+ <and> <IDENTIFIER>
}

void Var() #Initialization : {}
{
    LOOKAHEAD(RequiredVarLookahead()) DeclareLocalVar() <assign> Expression()
    |
    DeclareLocalVar() [ <assign> Expression() ]
}

void DeclareLocalVar() #Var :
{
    Token t;
}
{
    [ <FINAL> { jjtThis.setFinal(); } ]
    ( <VAR>
      |
      ( <INT> { jjtThis.setType(Integer.TYPE); }
        |
        <LONG> { jjtThis.setType(Long.TYPE); }
        |
        <SHORT> { jjtThis.setType(Short.TYPE); }
        |
        <BYTE> { jjtThis.setType(Byte.TYPE); }
        |
        <CHAR> { jjtThis.setType(Character.TYPE); }
        |
        <BOOLEAN> { jjtThis.setType(Boolean.TYPE); }
        |
        <FLOAT> { jjtThis.setType(Float.TYPE); }
        |
        <DOUBLE> { jjtThis.setType(Double.TYPE); }
        |
        LOOKAHEAD( { getToken(1).kind == IDENTIFIER && Parser.resolveType(getToken(1).image) != null } ) t=<IDENTIFIER> { jjtThis.setType(Parser.resolveType(t.image)); }

      ) (<LBRACKET> <RBRACKET> { jjtThis.setArray(); } )*
    )
    [ <and> { jjtThis.setRequired(); } ] t=<IDENTIFIER> { declareVariable(jjtThis, t); }
}

void DeclareExtVar(boolean isFinal) #ExtVar :
{
    Token t;
}
{
    { if (isFinal) jjtThis.setFinal(); }
    [ <and> { jjtThis.setRequired(); } ]
    t=<IDENTIFIER> { declareVariable(jjtThis, t); }
}

void DeclareTryVar() #Var :
{
    Token t;
}
{
    [ <FINAL> { jjtThis.setFinal(); } ]
    ( <VAR>
      |
      LOOKAHEAD( { getToken(1).kind == IDENTIFIER && Parser.resolveCloseableType(getToken(1).image) != null } ) t=<IDENTIFIER> { jjtThis.setType(Parser.resolveCloseableType(t.image)); }
    )
    t=<IDENTIFIER> { declareVariable(jjtThis, t); }
}

void DeclareCatchVar() #Var :
{
    Token t;
}
{
    [ <FINAL> { jjtThis.setFinal(); } ]
    ( <VAR>
      |
      LOOKAHEAD( { getToken(1).kind == IDENTIFIER && Parser.resolveThrowableType(getToken(1).image) != null } ) t=<IDENTIFIER> { jjtThis.setType(Parser.resolveThrowableType(t.image)); }
    )
    t=<IDENTIFIER> { declareVariable(jjtThis, t); }
}

void DeclareMultiCatchVar() #MultiVar :
{
    Token t;
}
{
    LOOKAHEAD( { getToken(1).kind == IDENTIFIER && Parser.resolveThrowableType(getToken(1).image) != null && getToken(2).kind == or} )
    t=<IDENTIFIER> { jjtThis.addType(Parser.resolveThrowableType(t.image)); }
    ( LOOKAHEAD({ getToken(1).kind == or && getToken(2).kind == IDENTIFIER && Parser.resolveThrowableType(getToken(2).image) != null})
        <or> t=<IDENTIFIER> { jjtThis.addType(Parser.resolveThrowableType(t.image)); }
    )+
    t=<IDENTIFIER> { declareVariable(jjtThis, t); }
}

void Pragma() #void :
{
    Token t;
    Object value;
}
{
    <PRAGMA> t=<QUALIFIED_IDENTIFIER> value=PragmaValue() { declarePragma(t.image, value); }
}

Object PragmaValue() #void :
{
    Token v;
}
{
      LOOKAHEAD(1) v=<INTEGER_LITERAL> { return NumberParser.parseInteger(v.image); }
    | LOOKAHEAD(1) v=<FLOAT_LITERAL> { return NumberParser.parseDouble(v.image); }
    | LOOKAHEAD(1) v=<STRING_LITERAL> { return Parser.buildString(v.image, true); }
    | LOOKAHEAD(1) v=<QUALIFIED_IDENTIFIER> { return v.image; }
    | LOOKAHEAD(1) <TRUE> { return true; }
    | LOOKAHEAD(1) <FALSE> { return false; }
    | LOOKAHEAD(1) <NULL> { return null; }
    | LOOKAHEAD(1) <NAN_LITERAL> { return Double.NaN; }
}


/***************************************
 *      Expression syntax
 ***************************************/

void Expression() #void : {}
{
    AssignmentExpression()
}

void AssignmentExpression() #void : {}
{
    ConditionalExpression()
    ( LOOKAHEAD(2) (
      <plus_assign> Expression() #SetAddNode(2)
      |
      <mult_assign> Expression() #SetMultNode(2)
      |
      <div_assign> Expression() #SetDivNode(2)
      |
      <mod_assign> Expression() #SetModNode(2)
      |
      <and_assign> Expression() #SetAndNode(2)
      |
      <or_assign> Expression() #SetOrNode(2)
      |
      <xor_assign> Expression() #SetXorNode(2)
      |
      <minus_assign> Expression() #SetSubNode(2)
      |
      <shl_assign> Expression() #SetShlNode(2)
      |
      <sar_assign> Expression() #SetSarNode(2)
      |
      <shr_assign> Expression() #SetShrNode(2)
      |
      <null_assign> Expression() #NullAssignment(2)
      |
      <ne_assign> Expression() #NEAssignment(2)
      |
      <assign> Expression() #Assignment(2)
    ) )*
}

/***************************************
 *      Conditional & relational
 ***************************************/

void ConditionalExpression() #void : {}
{
    ConditionalOrExpression()
    [ LOOKAHEAD(2) (
      <QMARK> TernaryExpression()
      |
      <ELVIS> Expression() #ElvisNode(2)
    ) ]
}

void TernaryExpression() #void : {}
{
    LOOKAHEAD(Expression() <COLON>) Expression() <COLON> Expression() #TernaryNode(3)
    |
    Expression() #TernaryNode(2)
}

void ConditionalOrExpression() #void : {}
{
    ConditionalAndExpression()
    ( LOOKAHEAD(2) (
       (<OR>|<_OR>) ConditionalAndExpression() #OrNode(2)
    ) )*
}

void ConditionalAndExpression() #void : {}
{
    InclusiveOrExpression()
    ( LOOKAHEAD(2) (
       (<AND>|<_AND>) InclusiveOrExpression() #AndNode(2)
    ) )*
}

void InclusiveOrExpression() #void : {}
{
    ExclusiveOrExpression()
    ( LOOKAHEAD(2) (
       <or> ExclusiveOrExpression() #BitwiseOrNode(2)
    ) )*
}

void ExclusiveOrExpression() #void : {}
{
    AndExpression()
    ( LOOKAHEAD(2) (
       <xor> AndExpression() #BitwiseXorNode(2)
    ) )*
}

void AndExpression() #void : {}
{
    EqualityExpression()
    ( LOOKAHEAD(2) (
       <and> EqualityExpression() #BitwiseAndNode(2)
    ) )*
}

void EqualityExpression() #void : {}
{
    RelationalExpression()
    [ LOOKAHEAD(2) (
       (<eq> | <EQ>) RelationalExpressionOperand() #EQNode(2)
     |
       (<ne> | <NE>) RelationalExpressionOperand() #NENode(2)
     |
       (<req> | <IN>) RelationalExpressionOperand() #ERNode(2) // equals regexp
     |
       <rne> RelationalExpressionOperand() #NRNode(2) // not equals regexp
     |
       <is> RangeExpression() #ISNode(2) // identical
     |
       <ni> RangeExpression() #NINode(2) // not identical

    ) ]
}

void SetOperand() : {}
{
    ( <QMARK> { jjtThis.setAny(); } | <NULLP> ) <LPAREN> RangeExpression() (<COMMA> RangeExpression())* <RPAREN>
}

void RelationalExpressionOperand() #void : {}
{
    SetOperand()
    |
    RangeExpression()
}

void RelationalExpression() #void : {}
{
    RangeExpression()
    [ LOOKAHEAD(2) (
      (<lt> |<LT>) RelationalExpressionOperand() #LTNode(2)
     |
      (<gt> | <GT>) RelationalExpressionOperand() #GTNode(2)
     |
      (<le> | <LE>) RelationalExpressionOperand() #LENode(2)
     |
      (<ge> | <GE>) RelationalExpressionOperand() #GENode(2)
     |
      <seq> RelationalExpressionOperand() #SWNode(2) // starts with
     |
      <sne> RelationalExpressionOperand() #NSWNode(2) // not starts with
     |
      <eeq> RelationalExpressionOperand() #EWNode(2) // ends with
     |
      <ene> RelationalExpressionOperand() #NEWNode(2) // not ends with
     |
      <iof> TypeReference() #IOFNode(2) // instanceof
     |
      <niof> TypeReference() #NIOFNode(2) // not instanceof

    ) ]
}

void RangeExpression() #void : {}
{
    ShiftExpression()
    ( LOOKAHEAD(2) (
       <range> ShiftExpression() #RangeNode(2) // range
    ) )?
}

/***************************************
 *      Arithmetic
 ***************************************/

void ShiftExpression() #void : {}
{
    AdditiveExpression()
    ( LOOKAHEAD(2) (
      <shl> AdditiveExpression() #ShiftLeftNode(2)
    |
      <shr> AdditiveExpression() #ShiftRightUnsignedNode(2)
    |
      <sar> AdditiveExpression() #ShiftRightNode(2)
    ) )*
}

void AdditiveExpression() #void : {}
{
    MultiplicativeExpression()
    ( LOOKAHEAD(2) (
      <plus> MultiplicativeExpression() #AddNode(2)
    |
      <minus> MultiplicativeExpression() #SubNode(2)
    ) )*
}

void MultiplicativeExpression() #void : {}
{
    UnaryExpression()
    ( LOOKAHEAD(2) (
      <mult> UnaryExpression() #MulNode(2)
    |
      (<div>|<DIV>) UnaryExpression() #DivNode(2)
    |
      (<mod>|<MOD>) UnaryExpression() #ModNode(2)
    ) )*
}

void UnaryExpression() #void : {}
{
    <minus> UnaryExpression() #UnaryMinusNode(1)
    |
    <unary_minus> UnaryExpression() #UnaryMinusNode(1)
    |
    <plus> UnaryExpression() #UnaryPlusNode(1)
    |
    <increment> UnaryExpression() #IncrementNode(1)
    |
    <decrement> UnaryExpression() #DecrementNode(1)
    |
    <mult> UnaryExpression() #IndirectNode(1)
    |
    <tilda> UnaryExpression() #BitwiseComplNode(1)
    |
    (<not>|<NOT>) UnaryExpression() #NotNode(1)
    |
    <EMPTY> UnaryExpression() #EmptyFunction(1)
    |
    <SIZE> UnaryExpression() #SizeFunction(1)
    |
    <ELLIPSIS> EnumerationExpression()
    |
    LOOKAHEAD(<LPAREN> TypeCastReference() <RPAREN> UnaryExpression()) (<LPAREN> TypeCastReference() <RPAREN> UnaryExpression() #CastNode(2))
    |
    PostfixExpression()
}

void PostfixExpression() #void : {}
{
    PointerExpression()
    ( LOOKAHEAD(1) (
      <increment> #IncrementPostfixNode(1)
    |
      <decrement> #DecrementPostfixNode(1)
    ) )*
}

void PointerExpression() #void : {}
{
    <and> ValueExpression() #PointerNode(1)
    |
    NullableExpression()
}

void EnumerationExpression() #void : {}
{
    ((IteratorExpression() (LOOKAHEAD(2) EnumerationAccess() )*) #EnumerationReference(>1)) #Reference(>1)
}

void EnumerationAccess() #void : {}
{
    LOOKAHEAD(<DOTC> ProjectionExpression() <COLON>) MapProjection()
    |
    LOOKAHEAD(<DOTC>) ArrayProjection()
    |
    LOOKAHEAD(<DOTB>) Selection()
}

void Selection() #SelectionNode : {}
{
    <DOTB> (
      StopCountSelection()
      |
      StartCountSelection()
      |
      ExpressionLambda()
    ) <RBRACKET>
}

void StopCountSelection() #StopCountNode : {}
{
    (<lt> | <LT>) Expression()
}

void StartCountSelection() #StartCountNode : {}
{
    (<gt> | <GT>) Expression()
}

void ArrayProjection() #ProjectionNode : {}
{
    <DOTC> ProjectionExpression() ( LOOKAHEAD(2) <COMMA> ProjectionExpression() )* <RCURLY>
}

void MapProjection() #MapProjectionNode : {}
{
    <DOTC> ProjectionExpression() <COLON> ProjectionExpression() <RCURLY>
}

void ProjectionExpression() #void : {}
{
    AttributeReference()
    |
    ExpressionLambda()
}

void AttributeReference() :
{
    Token t;
}
{
    t=<ANNOTATION> { jjtThis.setName(t.image); }
}

void IteratorExpression() #EnumerationNode : {}
{
    LOOKAHEAD(NullableExpression()) NullableExpression()
    |
    IteratorBlock()
}

void IteratorBlock() #SimpleLambda :
{
    pushFrame();
    pushUnit(jjtThis);
    branchScope.yieldReturnCount += 1;
}
{
    Block()
{
    branchScope.yieldReturnCount -= 1;
    popUnit(jjtThis);
    jjtThis.setScope(frame);
    popFrame();
}
}

void SwitchExpression() : {}
{
    <SWITCH> <LPAREN> Expression() <RPAREN> <LCURLY> { branchScope.yieldCount += 1; } SwitchExpressionBlock() { branchScope.yieldCount -= 1; } <RCURLY>
}

void SwitchExpressionBlock() #void : {}
{
    SwitchExpressionCase() (LOOKAHEAD(SwitchExpressionBlock()) SwitchExpressionBlock())*
    |
    SwitchExpressionDefault() (LOOKAHEAD(SwitchExpressionCase()) SwitchExpressionCase())*
}

void SwitchExpressionCase() : {}
{
    <CASE> SwitchCaseLabel() <LAMBDA> Statement()
}

void SwitchCaseLabel() : {}
{
    (Literal() | Identifier()) (<COMMA> (Literal() | Identifier()))*
}

void SwitchExpressionDefault() : {}
{
    <DCASE> <LAMBDA> Statement()
}

/***************************************
 *      Identifier & Literals
 ***************************************/

void Identifier(boolean top) :
{
    Token t;
}
{
    t=<IDENTIFIER> { jjtThis.setSymbol(top? checkVariable(jjtThis, t.image) : t.image); if (top && isFinalVariable(t.image)) jjtThis.setFinal(); }
    |
    t=<REGISTER> { jjtThis.setSymbol(t.image); }
}

void NamespaceIdentifier()  #NamespaceIdentifier :
{
    Token ns;
    Token id;
}
{
    ns=<IDENTIFIER> <COLON> id=<IDENTIFIER> { jjtThis.setNamespace(ns.image, id.image); }
}

void StringIdentifier() #Identifier :
{
    Token t;
}
{
    t=<STRING_LITERAL> { jjtThis.setSymbol(Parser.buildString(t.image, true));  }
}

void RemoveIdentifier() #Identifier :
{
    Token t;
}
{
    t=<REMOVE> { jjtThis.setSymbol(t.image); }
}

void MethodReferenceIdentifier() #Identifier :
{
    Token t;
}
{
    t=<SIZE> { jjtThis.setSymbol(t.image); }
    |
    t=<EMPTY> { jjtThis.setSymbol(t.image); }
    |
    t=<REMOVE> { jjtThis.setSymbol(t.image); }
    |
    t=<NEW> { jjtThis.setSymbol(t.image); }
    |
    t=<FUNCTION> { jjtThis.setSymbol(t.image); }
    |
    t=<VAR> { jjtThis.setSymbol(t.image); }
    |
    t=<YIELD> { jjtThis.setSymbol(t.image); }
    |
    t=<IDENTIFIER> { jjtThis.setSymbol(t.image); }
}

void This() #void : {}
{
    <THIS> #ThisNode
}

void Current() #void : {}
{
    <CURRENT> #CurrentNode
}

void Literal() #void : {}
{
    IntegerLiteral()
    |
    FloatLiteral()
    |
    BooleanLiteral()
    |
    JxltLiteral()
    |
    StringLiteral()
    |
    RegexLiteral()
    |
    NaNLiteral()
}

void UnderscoreLiteral() : {}
{
    <UNDERSCORE>
}

void NaNLiteral() #NumberLiteral : {}
{
    <NAN_LITERAL> { jjtThis.setReal("NaN"); }
}

void NullLiteral() : {}
{
    <NULL>
}

void BooleanLiteral() #void : {}
{
    <TRUE> #TrueNode
    |
    <FALSE> #FalseNode
}

void IntegerLiteral() #NumberLiteral :
{
    Token t;
}
{
    t=<INTEGER_LITERAL> { jjtThis.setNatural(t.image); }
}

void FloatLiteral() #NumberLiteral:
{
    Token t;
}
{
    t=<FLOAT_LITERAL> { jjtThis.setReal(t.image); }
}

void StringLiteral() :
{
    Token t;
}
{
    t=<STRING_LITERAL> { jjtThis.setLiteral(Parser.buildString(t.image, true)); }
}

void JxltLiteral() #JxltLiteral :
{
    Token t;
}
{
    t=<JXLT_LITERAL> { jjtThis.setLiteral(Parser.buildString(t.image, true)); }
}

void RegexLiteral() :
{
    Token t;
}
{
    t=<REGEX_LITERAL> { jjtThis.setLiteral(Parser.buildRegex(t.image)); }
}

void TypeCastReference() #ClassLiteral() :
{
    Token t;
}
{
    (
      t=<INT> { jjtThis.setLiteral(Integer.TYPE); }
      |
      t=<LONG> { jjtThis.setLiteral(Long.TYPE); }
      |
      t=<SHORT> { jjtThis.setLiteral(Short.TYPE); }
      |
      t=<BYTE> { jjtThis.setLiteral(Byte.TYPE); }
      |
      t=<CHAR> { jjtThis.setLiteral(Character.TYPE); }
      |
      t=<BOOLEAN> { jjtThis.setLiteral(Boolean.TYPE); }
      |
      t=<FLOAT> { jjtThis.setLiteral(Float.TYPE); }
      |
      t=<DOUBLE> { jjtThis.setLiteral(Double.TYPE); }
      |
      LOOKAHEAD( { getToken(1).kind == IDENTIFIER && Parser.resolveType(getToken(1).image) != null } ) t=<IDENTIFIER> { jjtThis.setLiteral(Parser.resolveType(t.image)); }
    )
    (LOOKAHEAD(<LBRACKET>) <LBRACKET> <RBRACKET> { jjtThis.setArray(); })*
}

void TypeReference() #ClassLiteral() :
{
    Token t;
    Class value;
}
{
    (LOOKAHEAD(<LBRACKET>) <LBRACKET> <RBRACKET> { jjtThis.setArray(); })+
    |
    (
        t=<QUALIFIED_IDENTIFIER> { value = Parser.resolveType(t.image); if (value == null) throwParsingException(jjtThis); jjtThis.setLiteral(value); }
      | t=<INT> { jjtThis.setLiteral(Integer.TYPE); }
      | t=<LONG> { jjtThis.setLiteral(Long.TYPE); }
      | t=<SHORT> { jjtThis.setLiteral(Short.TYPE); }
      | t=<BYTE> { jjtThis.setLiteral(Byte.TYPE); }
      | t=<CHAR> { jjtThis.setLiteral(Character.TYPE); }
      | t=<BOOLEAN> { jjtThis.setLiteral(Boolean.TYPE); }
      | t=<FLOAT> { jjtThis.setLiteral(Float.TYPE); }
      | t=<DOUBLE> { jjtThis.setLiteral(Double.TYPE); }
    )
    (LOOKAHEAD(<LBRACKET>) <LBRACKET> <RBRACKET> { jjtThis.setArray(); })*
}

void NewTypeReference() #ClassLiteral() :
{
    Token t;
    Class value;
}
{
    t=<QUALIFIED_IDENTIFIER> { value = Parser.resolveInstantiableType(t.image); if (value == null) throwParsingException(jjtThis); jjtThis.setLiteral(value); }
}

void ArrayTypeReference() #void : {}
{
    PrimitiveType()
    |
    ObjectType()
}

void ObjectType() #ClassLiteral() :
{
    Token t;
    Class value;
}
{
    t=<QUALIFIED_IDENTIFIER> { value = Parser.resolveType(t.image); if (value == null) throwParsingException(jjtThis); jjtThis.setLiteral(value); }
}

void InnerType() #Identifier() :
{
    Token t;
}
{
    t=<QUALIFIED_IDENTIFIER> { jjtThis.setSymbol(t.image); }
}

void PrimitiveType() #ClassLiteral() :
{
    Token t;
}
{
    t=<INT> { jjtThis.setLiteral(Integer.TYPE); }
    |
    t=<LONG> { jjtThis.setLiteral(Long.TYPE); }
    |
    t=<SHORT> { jjtThis.setLiteral(Short.TYPE); }
    |
    t=<BYTE> { jjtThis.setLiteral(Byte.TYPE); }
    |
    t=<CHAR> { jjtThis.setLiteral(Character.TYPE); }
    |
    t=<BOOLEAN> { jjtThis.setLiteral(Boolean.TYPE); }
    |
    t=<FLOAT> { jjtThis.setLiteral(Float.TYPE); }
    |
    t=<DOUBLE> { jjtThis.setLiteral(Double.TYPE); }
}

void EmptyListLiteral() #ArrayLiteral() : {}
{
    <LBRACKET> [ <ELLIPSIS> { jjtThis.setExtended(); } ] <RBRACKET>
}

void ArrayLiteral() : {}
{
    <LBRACKET>
        Expression() (LOOKAHEAD(<COMMA> Expression()) <COMMA> Expression() )* [LOOKAHEAD(2) <COMMA> <ELLIPSIS> { jjtThis.setExtended(); } ]
    <RBRACKET>
}

void ImmutableArrayLiteral() #ArrayLiteral() :
{
    jjtThis.setImmutable();
}
{
    <HBRACKET> [ Expression() (<COMMA> Expression() )* ] <RBRACKET>
}

void EmptyMapLiteral() #MapLiteral() : {}
{
    <LCURLY> <COLON> [ <ELLIPSIS> { jjtThis.setOrdered(); } ] <RCURLY>
}

void MapLiteral() : {}
{
    <LCURLY>
        MapElement() (LOOKAHEAD(<COMMA> MapElement()) <COMMA> MapElement() )* [LOOKAHEAD(2) <COMMA> <ELLIPSIS> { jjtThis.setOrdered(); } ]
    <RCURLY>
}

void MapElement() #void : {}
{
    LOOKAHEAD(<mult> <COLON> <ELLIPSIS>) <mult> <COLON> <ELLIPSIS> EnumerationExpression() #MapEnumerationNode(1)
    |
    MapEntry()
}

void MapEntry() : {}
{
    Expression() <COLON> Expression()
}

void MapEntryLiteral() : {}
{
    <LBRACKET> Expression() <COLON> Expression() <RBRACKET>
}

void ImmutableEmptyMapLiteral() #MapLiteral() :
{
    jjtThis.setImmutable();
}
{
    <HCURLY> <COLON> <RCURLY>
}

void ImmutableMapLiteral() #MapLiteral() :
{
    jjtThis.setImmutable();
}
{
    <HCURLY>
        MapElement() (LOOKAHEAD(<COMMA> MapElement()) <COMMA> MapElement() )* [LOOKAHEAD(2) <COMMA> <ELLIPSIS> { jjtThis.setOrdered(); } ]
    <RCURLY>
}

void EmptySetLiteral() #SetLiteral : {}
{
    <LCURLY> [ <ELLIPSIS> { jjtThis.setOrdered(); } ] <RCURLY>
}

void SetLiteral() : {}
{
    <LCURLY> Expression() ( LOOKAHEAD(<COMMA> Expression()) <COMMA> Expression() )* [ LOOKAHEAD(<COMMA> <ELLIPSIS> <RCURLY>) <COMMA> <ELLIPSIS> { jjtThis.setOrdered(); } ] <RCURLY>
}

void ImmutableEmptySetLiteral() #SetLiteral :
{
    jjtThis.setImmutable();
}
{
    <HCURLY> <RCURLY>
}

void ImmutableSetLiteral() #SetLiteral :
{
    jjtThis.setImmutable();
}
{
    <HCURLY> Expression() ( LOOKAHEAD(<COMMA> Expression()) <COMMA> Expression() )* [ LOOKAHEAD(<COMMA> <ELLIPSIS> <RCURLY>) <COMMA> <ELLIPSIS> { jjtThis.setOrdered(); } ] <RCURLY>
}

/***************************************
 *      Functions & Methods
 ***************************************/

void Arguments() #Arguments : {}
{
    <LPAREN> (Expression() (<COMMA> Expression())* )? <RPAREN>
}

void FunctionCallLookahead() #void : {}
{
    LOOKAHEAD(2) <IDENTIFIER> <COLON> <IDENTIFIER> <LPAREN>
    |
    LOOKAHEAD(2) <IDENTIFIER> <LPAREN>
    |
    LOOKAHEAD(2) <REGISTER> <LPAREN>
    |
    LOOKAHEAD(2) <REMOVE> <LPAREN>
}

void FunctionCall() #void : {}
{
    LOOKAHEAD(2) NamespaceIdentifier() Arguments() #FunctionNode(2)
    |
    LOOKAHEAD(2) Identifier(true) Arguments() #FunctionNode(2)
    |
    LOOKAHEAD(2) RemoveIdentifier() Arguments() #FunctionNode(2)
}

void Constructor() #void : {}
{
    <NEW>
    (
       LOOKAHEAD(<LPAREN>) ForNameConstructor()
       |
       LOOKAHEAD(<QUALIFIED_IDENTIFIER> <LPAREN>) QualifiedConstructor()
       |
       LOOKAHEAD(<QUALIFIED_IDENTIFIER> <LCURLY> MapElement()) InitializedMapConstructor()
       |
       LOOKAHEAD(<QUALIFIED_IDENTIFIER> <LCURLY>) InitializedCollectionConstructor()
       |
       LOOKAHEAD(ArrayTypeReference() <LBRACKET> <RBRACKET>) InitializedArrayConstructor()
       |
       LOOKAHEAD(ArrayTypeReference() <LBRACKET>) ArrayConstructor()
    )
}

void ForNameConstructor() #ConstructorNode() : {}
{
    <LPAREN> Expression() ( <COMMA> Expression() )* <RPAREN>
}

void QualifiedConstructor() #QualifiedConstructorNode() : {}
{
    NewTypeReference() Arguments()
}

void ArrayConstructor() #ArrayConstructorNode() : {}
{
    ArrayTypeReference() (LOOKAHEAD(2) ArrayQualifiedDimension())+ (LOOKAHEAD(2) ArrayOpenDimension())*
}

void ArrayQualifiedDimension() #void : {}
{
    <LBRACKET> Expression() <RBRACKET>
}

void ArrayOpenDimension() : {}
{
    <LBRACKET> <RBRACKET>
}

void InitializedArrayConstructor() #InitializedArrayConstructorNode() : {}
{
    ArrayTypeReference() <LBRACKET> <RBRACKET> InitializedCollectionLiteral()
}

void InitializedCollectionConstructor() #InitializedCollectionConstructorNode() : {}
{
    NewTypeReference() InitializedCollectionLiteral()
}

void InitializedMapConstructor() #InitializedMapConstructorNode() : {}
{
    NewTypeReference() InitializedMapLiteral()
}

void InitializedCollectionLiteral() #void() : {}
{
    <LCURLY> [ Expression() ( <COMMA> Expression() )* ] <RCURLY>
}

void InitializedMapLiteral() #void() : {}
{
    <LCURLY> [ MapElement() ( <COMMA> MapElement() )* ] <RCURLY>
}

void Parameter() #void :
{
    Token t;
}
{
    t=<IDENTIFIER> { declareParameter(t); }
}

void VarParameter() #void :
{
    Token t;
    Class type = null;
    boolean isFinal = false;
    boolean isRequired = false;
    Token v;
    Object value = null;
}
{
    [ <FINAL> { isFinal = true; } ]
    ( <VAR>
      |
      (
         <INT> { type = Integer.TYPE; }
         |
         <LONG> { type = Long.TYPE; }
         |
         <SHORT> { type = Short.TYPE; }
         |
         <BYTE> { type = Byte.TYPE; }
         |
         <CHAR> { type = Character.TYPE; }
         |
         <BOOLEAN> { type = Boolean.TYPE; }
         |
         <FLOAT> { type = Float.TYPE; }
         |
         <DOUBLE> { type = Double.TYPE; }
         |
         LOOKAHEAD( { getToken(1).kind == IDENTIFIER && Parser.resolveType(getToken(1).image) != null } ) t=<IDENTIFIER> { type = Parser.resolveType(t.image); }

      ) (LOOKAHEAD(<LBRACKET>) <LBRACKET> <RBRACKET> { type = Parser.arrayType(type); })*
    )
    [ <and> { isRequired = true; } ]
    t = <IDENTIFIER> 
    [ 
       <assign> 
       (
           v = <INTEGER_LITERAL> { value = NumberParser.parseInteger(v.image); }
           |
           v = <FLOAT_LITERAL> { value = NumberParser.parseDouble(v.image); }
           |
           v = <TRUE> { value = Boolean.TRUE; }
           |
           v = <FALSE> { value = Boolean.FALSE; }
           |
           v = <NULL> { value = null; }
           |
           v = <STRING_LITERAL> { value = Parser.buildString(v.image, true); }
           |
           v = <REGEX_LITERAL> { value = Pattern.compile(Parser.buildRegex(v.image)); }
           |
           v = <NAN_LITERAL> { value = Double.NaN; }
           |
           v = <IDENTIFIER> { value = resolveStaticField(v, type, v.image); }
       )
       |
       <ELLIPSIS> { declareVarArgSupport(); } 
    ]
    { declareParameter(t, type, isFinal, isRequired, value); }
}

void Parameters() #void : {}
{
    <LPAREN>
      [
        LOOKAHEAD(VarParameter()) VarParameter() (LOOKAHEAD( {getToken(1).kind == COMMA && !isVarArgs()} ) <COMMA> VarParameter())*
        |
        Parameter() (<COMMA> Parameter())* [ <ELLIPSIS> { declareVarArgSupport(); } ]
      ]
    <RPAREN>
}

void LambdaLookahead() #void() : {}
{
    LOOKAHEAD(3) [ <STATIC> ] <FUNCTION> <LPAREN>
    |
    LOOKAHEAD(3) [ <STATIC> ] <FUNCTION> <LCURLY>
    |
    [ <STATIC> ] Parameters() ( <LAMBDA> | <LAMBDAE> )
    |
    Parameter() ( <LAMBDA> | <LAMBDAE> )
}

void Lambda() #JexlLambda() :
{
    pushFrame();
    pushUnit(jjtThis);
}
{
    (
      LOOKAHEAD(3) [ <STATIC> { frame.declareStatic(); } ] <FUNCTION> Parameters() Block()
      |
      LOOKAHEAD(3) [ <STATIC> { frame.declareStatic(); } ] <FUNCTION> Block()
      |
      [ <STATIC> { frame.declareStatic(); } ] Parameters() ( <LAMBDA> Block() | <LAMBDAE> Expression() )
      |
      Parameter() ( <LAMBDA> Block() | <LAMBDAE> Expression() )
    )
{
    popUnit(jjtThis);
    jjtThis.setScope(frame);
    popFrame();
}
}


/***************************************
 *     References
 ***************************************/

Token dotName() #void :
{
    Token t;
}
{
    ( t = <DOT_IDENTIFIER> | t=<IF> | t=<ELSE> | t=<FOR> | t=<WHILE> | t=<DO> | t=<NEW> | t=<EMPTY> | t=<SIZE> | t=<TRUE> | t=<FALSE> | t=<NULL>
    | t=<TRY> | t=<CATCH> | t=<THROW> | t=<ASSERT> | t=<SYNCHRONIZED> | t=<REMOVE> | t=<THIS> | t=<YIELD>
    | t=<CHAR> | t=<BOOLEAN> | t=<BYTE> | t=<SHORT> | t=<INT> | t=<LONG> | t=<FLOAT> | t=<DOUBLE>
    | t=<SWITCH> | t=<CASE> | t=<DCASE> | t=<FINAL> | t=<IN> | t=<iof>
    | t=<_OR> | t=<_AND>| t=<NOT> | t=<NE> | t=<EQ> | t=<GT> | t=<GE> | t=<LT> | t=<LE>
    | t=<VAR> | t=<FUNCTION> | t=<STATIC> )
{
    return t;
}
}

void IdentifierAccess() #void :
{
    Token t;
}
{
    <DOT> (
        t=dotName() { jjtThis.setIdentifier(t.image); } #IdentifierAccess
        |
        t=<STRING_LITERAL> { jjtThis.setIdentifier(Parser.buildString(t.image, true)); } #IdentifierAccess
        |
        t=<JXLT_LITERAL> { jjtThis.setIdentifier(Parser.buildString(t.image, true)); } #IdentifierAccessJxlt
    )
    |
    <QDOT> (
        t=dotName() { jjtThis.setIdentifier(t.image); } #IdentifierAccessSafe
        |
        t=<STRING_LITERAL> { jjtThis.setIdentifier(Parser.buildString(t.image, true)); } #IdentifierAccessSafe
        |
        t=<JXLT_LITERAL> { jjtThis.setIdentifier(Parser.buildString(t.image, true)); } #IdentifierAccessSafeJxlt
    )
}

void ArrayAccess() : {}
{
    <LBRACKET> Expression() ( <COMMA> Expression() )* <RBRACKET>
}

void ArrayAccessSafe() : {}
{
    <QLBRACKET> Expression() ( <COMMA> Expression() )* <RBRACKET>
}

void ReferenceExpression() #MethodNode(>1) : {}
{
    ( <LPAREN> (
      LOOKAHEAD(Expression()) Expression() | { branchScope.yieldCount += 1; } Block() { branchScope.yieldCount -= 1; }
    ) <RPAREN> #EnclosedExpression(1) ) ( LOOKAHEAD(<LPAREN>) Arguments() )*
}

void PrimaryExpression() #void : {}
{
    LOOKAHEAD( LambdaLookahead() ) Lambda()
    |
    LOOKAHEAD( <LPAREN> ) ReferenceExpression()
    |
    LOOKAHEAD( EmptyMapLiteral() ) EmptyMapLiteral()
    |
    LOOKAHEAD( <LCURLY> MapElement() ) MapLiteral()
    |
    LOOKAHEAD( ImmutableEmptyMapLiteral() ) ImmutableEmptyMapLiteral()
    |
    LOOKAHEAD( <HCURLY> MapElement() ) ImmutableMapLiteral()
    |
    LOOKAHEAD( EmptySetLiteral() ) EmptySetLiteral()
    |
    LOOKAHEAD( <LCURLY> Expression() (<COMMA> | <RCURLY>)) SetLiteral()
    |
    LOOKAHEAD( ImmutableEmptySetLiteral() ) ImmutableEmptySetLiteral()
    |
    LOOKAHEAD( <HCURLY> Expression() (<COMMA> | <RCURLY>)) ImmutableSetLiteral()
    |
    LOOKAHEAD( EmptyListLiteral() ) EmptyListLiteral()
    |
    LOOKAHEAD( <LBRACKET> Expression() <COLON> ) MapEntryLiteral()
    |
    LOOKAHEAD( <LBRACKET> ) ArrayLiteral()
    |
    LOOKAHEAD( <HBRACKET> ) ImmutableArrayLiteral()
    |
    LOOKAHEAD( <NEW> ) Constructor()
    |
    LOOKAHEAD( <SWITCH> ) SwitchExpression()
    |
    LOOKAHEAD( FunctionCallLookahead() ) FunctionCall()
    |
    Identifier(true)
    |
    This()
    |
    Current()
    |
    Literal()
}

void NullableExpression() #void : {}
{
    (NullLiteral() | ValueExpression())
    ( LOOKAHEAD( <NULLP> )
       <NULLP> NullableExpression() #NullpNode(2)
    )*
}

void ValueExpression() #void : {}
{
    ( PrimaryExpression() ( LOOKAHEAD(2) MemberExpression() )* [LOOKAHEAD(2) MethodReference()] ) #Reference(>1)
}

void MemberExpression() #void : {}
{
    LOOKAHEAD(MethodCall()) MethodCall()
    |
    MemberAccess()
}

void MethodCall() #void : {}
{
    LOOKAHEAD(<DOT> <NEW> InnerType()) (<DOT> <NEW> InnerType() Arguments()) #InnerConstructorNode()
    |
    (MemberAccess() (LOOKAHEAD(<LPAREN>) Arguments())+) #MethodNode(>1)
}

void Pipe() #PipeNode : {}
{
    <DOTP> Expression() <RPAREN>
}

void MemberAccess() #void : {}
{
    LOOKAHEAD(<LBRACKET>) ArrayAccess()
    |
    LOOKAHEAD(<QLBRACKET>) ArrayAccessSafe()
    |
    LOOKAHEAD(<DOTP>) Pipe()
    |
    LOOKAHEAD(<DOT> | <QDOT>) IdentifierAccess()
    |
    LOOKAHEAD(<LCURLY>) InlinePropertyAssignment()
}

void MethodReference() : {}
{
    <DCOLON> MethodReferenceIdentifier()
}

void InlinePropertyAssignment() : {}
{
    <LCURLY> (InlinePropertyBlock() ( <COMMA> InlinePropertyBlock() )* ) <RCURLY>
}

void InlineMemberAccess() #void : {}
{
    LOOKAHEAD(<LBRACKET>) ArrayAccess()
    |
    LOOKAHEAD(<DOT>) IdentifierAccess()
}

void InlinePropertyBlock() #void : {}
{
    LOOKAHEAD(InlinePropertyName() <COLON>) InlinePropertyEntry()
    |
    LOOKAHEAD(InlinePropertyName() <ELVIS>) InlinePropertyNullEntry()
    |
    LOOKAHEAD(InlinePropertyName() <EN_ASSIGN>) InlinePropertyNEEntry()
    |
    LOOKAHEAD(<LBRACKET> Expression() <RBRACKET> <COLON>) InlinePropertyArrayEntry()
    |
    LOOKAHEAD(<LBRACKET> Expression() <RBRACKET> <ELVIS>) InlinePropertyArrayNullEntry()
    |
    LOOKAHEAD(<LBRACKET> Expression() <RBRACKET> <EN_ASSIGN>) InlinePropertyArrayNEEntry()
    |
    ((LOOKAHEAD(<LBRACKET>) ArrayAccess() | Identifier()) (LOOKAHEAD(2) InlineMemberAccess() )* InlinePropertyAssignment()) #Reference()
}

void InlinePropertyName() #void : {}
{
    Identifier()
    |
    StringLiteral()
    |
    JxltLiteral()
}

void InlinePropertyEntry() : {}
{
    InlinePropertyName() <COLON> Expression()
}

void InlinePropertyNullEntry() : {}
{
    InlinePropertyName() <ELVIS> Expression()
}

void InlinePropertyNEEntry() : {}
{
    InlinePropertyName() <EN_ASSIGN> Expression()
}

void InlinePropertyArrayEntry() : {}
{
    <LBRACKET> Expression() <RBRACKET> <COLON> Expression()
}

void InlinePropertyArrayNullEntry() : {}
{
    <LBRACKET> Expression() <RBRACKET> <ELVIS> Expression()
}

void InlinePropertyArrayNEEntry() : {}
{
    <LBRACKET> Expression() <RBRACKET> <EN_ASSIGN> Expression()
}
